plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'base'
apply plugin: 'maven-publish'

group 'fi.jarimatti.karaf'
version '0.0.2-SNAPSHOT'

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    publications = ['featurePublication']

    pkg {
        repo = 'maven'
        name = 'akka-features'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/jarimatti/akka-features.git'

        version {
            name = '0.0.2'
            released = new Date()
            vcsTag = '0.0.2'
        }
    }
}

publishing {
    publications {
        featurePublication(MavenPublication) {
            artifact source: "$buildDir/karaf/features/features.xml",
                    classifier: "features",
                    extension: "xml"

            pom.withXml() {
                asNode().appendNode('description', 'Akka feature repository for Karaf.')
            }
        }
    }
}

project.ext {
    scala_version = "2.11.8"
    scala_main_version = "2.11"
    scala_java8_compat_version = "0.7.0"
    akka_version = "2.4.11"

    typesafe_config_version = "1.3.0"
    ssl_config_version = "0.2.1"
    reactive_streams_version = "1.0.0"
}

task generateFeatures(type: Copy) {
    group 'Karaf'
    description 'Generate filtered features.xml with version numbers.'

    from "src/karaf/features"
    into "$buildDir/karaf/features"

    expand(project.properties)
}

assemble.dependsOn generateFeatures